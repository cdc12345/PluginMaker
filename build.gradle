import groovy.json.JsonSlurper

plugins {
    id "java"
}

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" } // for gradle-tooling-api
    flatDir { dirs mcreator_path + '/lib' }
    maven {
        url "https://jitpack.io"
    }
}

group = 'net.mcreator.demojavaplugin'
version = new JsonSlurper().parse(file('src/main/resources/plugin.json'))['info']['version']

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

configurations {
    implementation.extendsFrom export
}

dependencies {
    implementation project(':MCreator')

    project(':MCreator').afterEvaluate(() ->
            project(":MCreator").configurations.named("implementation").get().dependencies.each {
                //noinspection ForeignDelegate
                implementation it
            }
    )

    // Use "export" configuration for dependencies that should be included in the plugin (if MCreator does not provide them)
    // export group: 'org.example', name: 'example-library', version: '1.2.3'
    implementation 'com.github.OVNSAME:Modding:1.5.0'
}

tasks.jar {
    archiveFileName.set("plugin-generator.zip")

    from {
        configurations.export.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.register('runMCreatorWithPlugin', JavaExec) {
    dependsOn jar

    environment("MCREATOR_PLUGINS_FOLDER", file("./build/libs"))

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    classpath = project(':MCreator').sourceSets.main.runtimeClasspath

    mainClass = 'net.mcreator.Launcher'

    workingDir = mcreator_path
}

subprojects { subproject ->
    subproject.plugins.withType(JavaPlugin) {
        test {
            dependsOn rootProject.jar

            environment("MCREATOR_PLUGINS_FOLDER", new File(rootProject.projectDir, "/build/libs"))

            workingDir = mcreator_path
        }
    }
}

tasks.register('exportToMyMCreator', Copy) {
    dependsOn jar
    from("build/libs/demo-plugin.zip")
    destinationDir(new File(getUserFolder(), "plugins"))
}

private static File getUserFolder() {
    if (System.getenv("MCREATOR_HOME") != null) {
        return new File(System.getenv("MCREATOR_HOME"));
    }

    return new File(System.getProperty("user.home") + "/.mcreator/");
}